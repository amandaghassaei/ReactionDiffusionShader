<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <title>Reaction-Diffusion Shader</title>

    <link rel="stylesheet" type="text/css" href="dependencies/bootstrap.min.css">
    <link rel="stylesheet" type="text/css" href="dependencies/flat-ui.min.css">
    <link rel="stylesheet" type="text/css" href="main.css">

    <script id="2d-vertex-shader" type="x-shader/x-vertex">

        attribute vec2 a_position;

        void main() {
           gl_Position = vec4(a_position, 0, 1);
        }
    </script>

    <script id="2d-render-shader" type="x-shader/x-fragment">
        precision mediump float;

        uniform sampler2D u_image;
        uniform vec2 u_textureSize;
        uniform float u_scale;

        float COLOR_MIN = 0.2, COLOR_MAX = 0.4 + 0.6*(1.0-u_scale);

        float bilinearInterp(vec2 pos, sampler2D texture, vec2 size){
            //bilinear interp between nearest cells
            vec2 pxCenter = vec2(0.5, 0.5);
            vec2 ceiled = ceil(pos);
            vec2 floored = floor(pos);
            float n = texture2D(texture, (ceiled+pxCenter)/size).y;//actually ne
            float s = texture2D(texture, (floored+pxCenter)/size).y;//actually sw
            if (ceiled.x != floored.x){
                float se = texture2D(texture, (vec2(ceiled.x, floored.y)+pxCenter)/size).y;
                float nw = texture2D(texture, (vec2(floored.x, ceiled.y)+pxCenter)/size).y;
                n = n*(pos.x-floored.x) + nw*(ceiled.x-pos.x);
                s = se*(pos.x-floored.x) + s*(ceiled.x-pos.x);
            }
            float materialVal = n;
            if (ceiled.y != floored.y){
                materialVal = n*(pos.y-floored.y) + s*(ceiled.y-pos.y);
            }
            return materialVal;
        }

        void main() {

            vec2 scaledCoord = (gl_FragCoord.xy-vec2(0.5, 0.5))*u_scale;
            vec2 scaledSize = u_textureSize*u_scale;

            float val = 1.0/u_scale*bilinearInterp(vec2(1.0, 1.0) + scaledCoord/scaledSize*(scaledSize-vec2(0.5, 0.5)/u_scale), u_image, scaledSize);

            float v = (COLOR_MAX - val) / (COLOR_MAX - COLOR_MIN);
            gl_FragColor = vec4(v, v, v, 1);
        }
    </script>

    <script id="2d-fragment-shader" type="x-shader/x-fragment">
        precision mediump float;
        #define M_PI 3.1415926535897932384626433832795

        //texture array
        uniform sampler2D u_image;//u, v, --, -- = r, g, b, a
        uniform sampler2D u_sinks;

        uniform vec2 u_textureSize;

        uniform vec2 u_posPosition;
        uniform vec2 u_negPosition;

        uniform vec2 u_mouseCoord;
        uniform float u_mouseEnable;

        //dots
        const float fParam = 0.0367;
        const float kParam = 0.065;
        const float DuParam = 0.24;
	    const float DvParam = 0.12;

        //classic
        //const float fParam = 0.0545;
        //const float kParam = 0.062;
        //const float DuParam = 0.2;
	    //const float DvParam = 0.1;

        //const float fParam = 0.0300;
        //const float kParam = 0.0625;

	    const float dt = 1.0;

        float rand(vec2 co){
            return fract(sin(dot(co.xy, vec2(12.9898,78.233))) * 43758.5453);
        }

        vec2 addPointCharge(vec2 position, vec2 fragCoord){
            vec2 vectToCharge = fragCoord-position;
            float angle = atan(vectToCharge.y, vectToCharge.x);//-pi to pi
            float mag = log(length(vectToCharge));
            if (mag > 0.0) mag = 6.0/mag;
            else mag = 0.0;
            return vec2(mag, angle);
        }

        void main() {

            vec2 fragCoord = gl_FragCoord.xy;

            vec2 currentState = texture2D(u_image, fragCoord/u_textureSize).xy;
            float u = currentState.x;
            float v = currentState.y;

            vec2 n = texture2D(u_image, (fragCoord + vec2(0.0, 1.0))/u_textureSize).xy;//north
            vec2 s = texture2D(u_image, (fragCoord + vec2(0.0, -1.0))/u_textureSize).xy;//south
            vec2 e = texture2D(u_image, (fragCoord + vec2(-1.0, 0.0))/u_textureSize).xy;//east
            vec2 w = texture2D(u_image, (fragCoord + vec2(1.0, 0.0))/u_textureSize).xy;//west

            vec2 ne = texture2D(u_image, (fragCoord + vec2(-1.0, 1.0))/u_textureSize).xy;//northeast
            vec2 se = texture2D(u_image, (fragCoord + vec2(-1.0, -1.0))/u_textureSize).xy;//southeast
            vec2 nw = texture2D(u_image, (fragCoord + vec2(1.0, 1.0))/u_textureSize).xy;//northwest
            vec2 sw = texture2D(u_image, (fragCoord + vec2(1.0, -1.0))/u_textureSize).xy;//southwest

            if (u_mouseEnable == 1.0){
                vec2 pxDistFromMouse = (fragCoord - u_mouseCoord)*(fragCoord - u_mouseCoord);
                float tol = 10.0;
                if (length(pxDistFromMouse)<tol){
                    vec2 invTextureSize = vec2(u_textureSize.y, u_textureSize.x);
                    gl_FragColor = vec4(0.5 + rand(u_textureSize) * 0.02 - 0.01, 0.25 + rand(invTextureSize) * 0.02 - 0.01, 0, 1);
                    return;
                }
            }

            //rotate diffusion to proper frame

            //vec2 magAnglePos = addPointCharge(u_posPosition, fragCoord);
            //vec2 magAngleNeg = addPointCharge(u_negPosition, fragCoord);

            //vec2 orientation = vec2(0.5, 0.5);
            //orientation[0] += magAnglePos[0] + magAngleNeg[0];
            //float sum = orientation[0] + orientation[1];
            //orientation /= sum;

            //float angle = magAnglePos[1] + magAngleNeg[1];

            float angle = 0.0;
            vec2 orientation = vec2(0.5, 0.5);
            float sum = 0.0;
            vec2 sinksSize = vec2(32, 2);
            for (float j=0.0;j<32.0;j++){
                vec4 sinks = texture2D(u_sinks, vec2(j/sinksSize.x, 0.5/2.0));
                if (sinks.x == -1.0) break;

                vec2 sink1 = addPointCharge(sinks.xy, fragCoord);

                vec2 sink2 = vec2(0);
                if (sinks[2] >= 0.0) sink2 = addPointCharge(vec2(sinks[2], sinks[3]), fragCoord);

                orientation[0] += sink1[0] + sink2[0];
                sum += orientation[0] + orientation[1];

                angle += sink1[1] + sink2[1];
            }
            orientation /= sum;


            float cosAngle = cos(angle);
            float sinAngle = sin(angle);
            mat2 diffRot = mat2(vec2(orientation[0]*cosAngle*cosAngle + orientation[1]*sinAngle*sinAngle,
                (orientation[1]-orientation[0])*cosAngle*sinAngle),
                vec2((orientation[1]-orientation[0])*cosAngle*sinAngle,
                orientation[1]*cosAngle*cosAngle + orientation[0]*sinAngle*sinAngle));

            float dxx = diffRot[0][0];
            float dxy = diffRot[0][1];
            float dyy = diffRot[1][1];

            vec3 DvMat1 = vec3(-1.0*DvParam*dxy, 2.0*DvParam*dyy, DvParam*dxy);
            vec3 DvMat2 = vec3(2.0*DvParam*dxx, -4.0*DvParam*(dxx+dyy), 2.0*DvParam*dxx);
            vec3 DvMat3 = vec3(DvParam*dxy, 2.0*DvParam*dyy, -1.0*DvParam*dxy);

            vec3 DuMat1 = vec3(-1.0*DuParam*dxy, 2.0*DuParam*dyy, DuParam*dxy);
            vec3 DuMat2 = vec3(2.0*DuParam*dxx, -4.0*DuParam*(dxx+dyy), 2.0*DuParam*dxx);
            vec3 DuMat3 = vec3(DuParam*dxy, 2.0*DuParam*dyy, -1.0*DuParam*dxy);

            vec3 vMat1 = vec3(nw.y, n.y, ne.y);
            vec3 vMat2 = vec3(w.y, currentState.y, e.y);
            vec3 vMat3 = vec3(sw.y, s.y, se.y);

            vec3 uMat1 = vec3(nw.x, n.x, ne.x);
            vec3 uMat2 = vec3(w.x, currentState.x, e.x);
            vec3 uMat3 = vec3(sw.x, s.x, se.x);

            float convolveV = dot(vMat1, DvMat1) + dot(vMat2, DvMat2) + dot(vMat3, DvMat3);
            float convolveU = dot(uMat1, DuMat1) + dot(uMat2, DuMat2) + dot(uMat3, DuMat3);

            float uvSq = u*v*v;
            vec2 delta = dt*vec2(convolveU - uvSq + fParam*(1.0 - u),
                convolveV + uvSq - (fParam + kParam)*v);

            gl_FragColor = vec4(u + delta.x, v + delta.y, 0, 1);
        }
    </script>

    <script type="text/javascript" src="dependencies/jquery-3.1.0.min.js"></script>
    <script type="text/javascript" src="dependencies/flat-ui.min.js"></script>

    <script type="text/javascript" src="GlBoilerplate.js"></script>
    <script type="text/javascript" src="rxndiffusion.js"></script>
</head>
<body>

<canvas id="glcanvas"></canvas>

<a href="#" id="about">?</a>

<div class="modal fade" id="aboutModal" tabindex="-1" role="dialog" aria-labelledby="basicModal" aria-hidden="true">
    <div class="modal-dialog modal-lg">
        <div class="modal-content">
            <div class="modal-body">
                <b>Reaction-Diffusion Shader</b><br/><br/>
                This is a simulation of a <a href="https://en.wikipedia.org/wiki/Reaction%E2%80%93diffusion_system" target="_blank">Gray-Scott reaction-diffusion system</a>, running in a GPU shader.
                The parameters for this model are F = 0.0545 and K = 0.062. With these parameters the system forms a mitosis-like pattern, where small cells divide and spread across space.
                Reaction diffusion patterns are interesting, but they can be difficult to control in meaningful ways for design purposes.
                In this project I added an underlying vector field that controls the orientation of diffusion across the system to produce directed, global patterns.
                Click on the screen to change the location of the sinks in the vector field.
                <br/><br/>
                The math used to created oriented diffusion in this system is discussed in the papers <a href="https://www.sci.utah.edu/publications/SCITechReports/UUSCI-2003-002.pdf" target="_blank">Display of Vector Fields Using a Reaction-Diffusion Model</a>
                and <a href="http://www.cs.cmu.edu/~jkh/462_s07/reaction_diffusion.pdf" target="_blank">Reaction-Diffusion Textures</a>.
                More info about ways to control these systems can also be found on <a href="http://www.karlsims.com/rd.html" target="_blank">Karl Sims' Webpage</a>.
                Information about programming a reaction diffusion system on the GPU is here: <a href="https://bl.ocks.org/robinhouston/ed597847175cf692ecce" target="_blank">A reaction-diffusion simulation using WebGL</a>.
                If this simulation is not performing well on your computer, resize your browser's window to make it smaller.
                <br/><br/>
                By <a href="http://www.amandaghassaei.com/" target="_blank">Amanda Ghassaei</a>, code on <a href="https://github.com/amandaghassaei/ReactionDiffusionShader" target="_blank">Github</a>.
                <br/><br/>
            </div>
        </div>
    </div>
</div>

</body>
</html>